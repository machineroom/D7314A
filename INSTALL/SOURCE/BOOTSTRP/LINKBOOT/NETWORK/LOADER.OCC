PROC Loader ([4]CHAN OF ANY   links,
             CHAN OF ANY      bootlink.in, bootlink.out,
             [4]BYTE          Memory,
             VAL INT          Buffer.address,
             INT              Next.address,
             INT              Entry.point,
             INT	      Data.point)


  --{{{  Constants
  VAL   MEMORY.PATTERN     IS        BYTE #FF :
  VAL   TWO.KAY            IS        2048 : 
  VAL   data.field         IS        #3F :
  VAL   data.field.bits    IS          6 :
  VAL   tag.field          IS        #C0 :
  VAL   tag.field.bits     IS          2 :
  VAL   message            IS          0 :
  VAL   number             IS          1 :
  VAL   operate            IS          2 :
  VAL   prefix             IS          3 :
  VAL   tag.prefix         IS   prefix << data.field.bits :
  VAL   message.length     IS         60 :
  
  VAL   load               IS          0 :
  VAL   pass               IS          1 :
  VAL   open               IS          2 :
  VAL   operate.open       IS   BYTE ((operate << data.field.bits) \/ open) :
  VAL   close              IS          3 :
  VAL   operate.close      IS   BYTE ((operate << data.field.bits) \/ close) :
  VAL   address            IS          4 :
  VAL   execute            IS          5 :
  VAL   Data.position	   IS          6 :
  VAL   operate.execute    IS  BYTE ((operate << data.field.bits) \/ execute) :
  
  VAL   operate.data.postion IS  BYTE ((operate << data.field.bits) \/ Data.position) :
  VAL   code.load          IS         7 :
  VAL   operate.code.load  IS         BYTE ((operate << data.field.bits) \/ code.load) :
  
  VAL   code.address       IS         8 :
  VAL   operate.code.address IS       BYTE ((operate << data.field.bits) \/ code.address) :
  
  VAL  data.load           IS        9 :
  VAL  operate.data.load   IS        BYTE ((operate << data.field.bits) \/ data.load) :
  
  VAL  data.address        IS        10 :
  VAL  operate.data.address  IS      BYTE ((operate << data.field.bits) \/ data.address) :
  
  VAL  Entry.position      IS        11 :
  VAL  operate.entry.position IS     BYTE ((operate << data.field.bits) \/ Entry.position) : 
  
  VAL  Bootstrap.load      IS        12 :
  VAL  Operate.bootstrap.load IS       BYTE ((operate << data.field.bits) \/ Bootstrap.load) : 
  
  VAL  Bootstrap.end      IS        13 :
  VAL  Operate.bootstrap.end  IS      BYTE ((operate << data.field.bits) \/ Bootstrap.end) : 
  
  VAL  Memory.size        IS        14 :
  VAL  Operate.memory.size    IS      BYTE ((operate << data.field.bits) \/ Memory.size) :
  
  --}}}
  --{{{  Variables
  BYTE   command :
  INT    Bootstrap.depth, links.to.load, last.address, output.link :
  INT    tag, operand :
  BOOL   More, loading :
  --}}}
  --{{{  PROC Get.encoded.value (INT Address)
  PROC Get.encoded.value (INT Address)
    SEQ
      Address := 0
      More := TRUE
      WHILE More    
        SEQ
          Address := Address << data.field.bits
          bootlink.in ? command
          Address := Address PLUS ((INT command) /\ data.field)
          More := (INT command) >= tag.prefix
  :
  --}}}
  --{{{  INLINE PROC Clear.memory.32 (VAL INT32 Memory.size, VAL BYTE Value)
  INLINE PROC Clear.memory.32 (VAL INT32 Memory.size, VAL BYTE Value)
  
    --{{{  procedures
    --{{{  PROC ClearByteBuffer
    PROC ClearByteBuffer ([]BYTE Buffer, VAL BYTE Value)
    
      --{{{  variables
      INT Index, Count :
      --}}}
    
      --{{{  main body
      SEQ
        --{{{  initialise first element
        Count := 1
        Index := Count
        
        Buffer[0] := Value
        --}}}
      
        WHILE Index < (SIZE Buffer)
          SEQ
            --{{{  copy buffer onto itself
            --{{{  calculate size to copy
            IF
              Count > ((SIZE Buffer) MINUS Index)
                --{{{  near end of buffer
                Count := (SIZE Buffer) MINUS Index
                --}}}
              TRUE
                --{{{  in middle of buffer
                SKIP
                --}}}
            --}}}
            
            [Buffer FROM Index FOR Count] := [Buffer FROM 0 FOR Count]
            --}}}
        
            --{{{  update buffer positions
            Index := Index PLUS Count
            Count := Count PLUS Count
            --}}}
      --}}}
    
    :
    --}}}
    --}}}
  
    --{{{  variables
    [MOSTPOS INT]BYTE BtMemory :
    PLACE BtMemory AT 0 :
    
    INT32 Free.start :
    --}}}
  
    --{{{  main body
    SEQ
      --{{{  calculate free memory start
      Free.start := INT32 (TWO.KAY)
      --}}}
      IF
        Memory.size > Free.start
          --{{{  sufficient memory
          WHILE Free.start < Memory.size
            IF
              (Memory.size MINUS Free.start) > (INT32 (MOSTPOS INT))
                --{{{  more than MOSTPOS INT left
                SEQ
                  --{{{  clear memory segment
                  ClearByteBuffer([BtMemory FROM INT Free.start FOR INT (MOSTPOS INT)], Value)
                  --}}}
                
                  Free.start := Free.start PLUS (INT32 (MOSTPOS INT))
                --}}}
              TRUE
                --{{{  less than MOSTPOS INT left
                SEQ
                  --{{{  clear memory segment
                  ClearByteBuffer([BtMemory FROM INT Free.start FOR INT (Memory.size MINUS Free.start)], Value)
                  --}}}
                
                  Free.start := Memory.size
                --}}}
          --}}}
        TRUE
          --{{{  insufficient memory
          SKIP
          --}}}
    --}}}
  
  :
  
  
  --}}}
  --{{{  INLINE PROC Clear memory
  --{{{  PROC Clear.memory (VAL INT Memory.size, VAL BYTE Value)
  INLINE PROC Clear.memory (VAL INT Memory.size, VAL BYTE Value)
  
    --{{{  procedures
    --{{{  PROC ClearByteBuffer
    PROC ClearByteBuffer ([]BYTE Buffer, VAL BYTE Value)
    
      --{{{  variables
      INT Index, Count :
      --}}}
    
      --{{{  main body
      SEQ
        --{{{  initialise first element
        Count := 1
        Index := Count
        
        Buffer[0] := Value
        --}}}
      
        WHILE Index < (SIZE Buffer)
          SEQ
            --{{{  copy buffer onto itself
            --{{{  calculate size to copy
            IF
              Count > ((SIZE Buffer) MINUS Index)
                --{{{  near end of buffer
                Count := (SIZE Buffer) MINUS Index
                --}}}
              TRUE
                --{{{  in middle of buffer
                SKIP
                --}}}
            --}}}
            
            [Buffer FROM Index FOR Count] := [Buffer FROM 0 FOR Count]
            --}}}
        
            --{{{  update buffer positions
            Index := Index PLUS Count
            Count := Count PLUS Count
            --}}}
      --}}}
    
    :
    --}}}
    --}}}
  
    --{{{  variables
    [MOSTPOS INT]BYTE BtMemory :
    PLACE BtMemory AT 0 :
    
    INT Free.start :
    --}}}
  
    --{{{  main body
    SEQ
      --{{{  calculate free memory start
      Free.start := INT (TWO.KAY)
      --}}}
      IF
        Memory.size > Free.start
          --{{{  sufficient memory
          WHILE Free.start < Memory.size
            IF
              (Memory.size MINUS Free.start) > (INT (MOSTPOS INT))
                --{{{  more than MOSTPOS INT left
                SEQ
                  --{{{  clear memory segment
                  ClearByteBuffer([BtMemory FROM INT Free.start FOR INT (MOSTPOS INT)], Value)
                  --}}}
                
                  Free.start := Free.start PLUS (INT (MOSTPOS INT))
                --}}}
              TRUE
                --{{{  less than MOSTPOS INT left
                SEQ
                  --{{{  clear memory segment
                  ClearByteBuffer([BtMemory FROM INT Free.start FOR INT (Memory.size MINUS Free.start)], Value)
                  --}}}
                
                  Free.start := Memory.size
                --}}}
          --}}}
        TRUE
          --{{{  insufficient memory
          SKIP
          --}}}
    --}}}
  
  :
  --}}}
  --}}}
  --{{{  INLINE PROC Get.encoded.value.32 (INT32 Address)
  INLINE PROC Get.encoded.value.32 (INT32 Address)
    SEQ
      Address := INT32 (0)
      More := TRUE
      WHILE More    
        SEQ
          Address := Address << data.field.bits
          bootlink.in ? command
          Address := Address PLUS (INT32 ((INT command) /\ data.field))
          More := (INT command) >= tag.prefix
  :
    
  
  --}}}
  
  SEQ
    bootlink.in ? command
    --{{{  While continue
    WHILE command <> operate.execute
      --  process command
      SEQ
        --{{{  unpack command
        tag := (INT command) >> data.field.bits
        operand := (INT command) /\ data.field
        --}}}
        IF
          --{{{  Tag = message
          tag = message
            INT   load.address :
            SEQ
              --{{{  Set up address
              IF
                --{{{  already loading 
                loading
                  SEQ
                    --{{{  Bump address
                    load.address := last.address
                    last.address := load.address PLUS operand
                    --}}}
                --}}}
                --{{{  Reset address
                TRUE
                  load.address := Buffer.address
                --}}}
              --}}}
              --{{{  Read in message and output
              --{{{  Read in message
              IF
                operand <> 0
                  bootlink.in ? [Memory FROM load.address FOR operand]
                TRUE
                  SKIP
              --}}}
              --{{{  Send messages to all open links
              SEQ i = 0 FOR 4
                IF
                  (links.to.load /\ (1 << i)) <> 0
                    SEQ
                      links[i] ! command			
                      IF
                        operand <> 0
                          links[i] ! [Memory FROM load.address FOR operand]
                        TRUE
                          SKIP
                  TRUE
                    SKIP
              --}}}
              --}}}
          --}}}
          --{{{  Tag = operate
          tag = operate
            --
            -- A large number of IFs because CASE will generate
            -- possibly more code but less test. But we are only
            -- interested in keeping the code size to a minimum
            --
            IF
              --{{{  operand = PASS
              operand = pass
                --
                -- Used to inform the current processor NOT to process any commands
                -- but to pass them on until told otherwise
                --
                SEQ
                  --{{{  Set loading to FALSE and close all LINK.TO.LOAD
                  loading := FALSE
                  links.to.load := 0
                  --}}}
              --}}}
              --{{{  operand = OPEN
              operand = open
                INT   depth :
                SEQ
                  depth := 1
                  WHILE depth <> 0
                    SEQ
                      bootlink.in ? command
                      IF
                        command = operate.open
                          depth := depth PLUS 1
                        command = operate.close
                          depth := depth MINUS 1
                        TRUE
                          SKIP
                      IF
                        depth <> 0
                          links[output.link] !  command
                        TRUE
                          SKIP
              --}}}
              --{{{  operand = ADDRESS
              operand = address
                SEQ
                  Get.encoded.value (last.address)
                  Next.address := last.address
              --}}}
              --{{{  operand = DATA.POSITION
              operand = Data.position
                SEQ
                  Get.encoded.value (Data.point)
              --}}}
              --{{{  operand = ENTRY.POSITION
              operand = Entry.position
                SEQ
                  Get.encoded.value (Entry.point)
              --}}}
              --{{{  operand = CODE.ADDRESS
              operand = code.address
                SEQ
                  Get.encoded.value (last.address)
                  Entry.point := last.address
              --}}}
              --{{{  operand = DATA.ADDRESS
              operand = data.address
                SEQ
                  Get.encoded.value (last.address)
                  Data.point := last.address
              --}}}
              --{{{  operand = BOOTSTRAP.LOAD
              operand = Bootstrap.load
                INT   load.address :
                INT   Bootstrap.length :
                SEQ
                  Bootstrap.depth := 0
                  Bootstrap.length := 0
                  load.address := Buffer.address
                  More := TRUE
                  bootlink.in ? command
                  More := (INT command) >= data.field
                  WHILE More
                    SEQ
                      Bootstrap.depth := Bootstrap.depth PLUS 1
                      SEQ i = 0 FOR 4
                        IF
                          (links.to.load /\ (1 << i)) <> 0
                            SEQ
                              links[i] ! command
                          TRUE
                            SKIP
                      bootlink.in ? command
                      More := (INT command) >= data.field
              
                  operand := (INT command) /\ data.field
              
                  IF 
                    Bootstrap.depth > 0                 
                      --  read in message
                      SEQ
                        IF
                          operand <> 0
                            bootlink.in ? [Memory FROM load.address FOR operand]
                          TRUE
                            SKIP
                        --  send message to outputs
                        SEQ i = 0 FOR 4
                          IF
                            (links.to.load /\ (1 << i)) <> 0
                              SEQ
                                links[i] ! command
                                IF
                                  operand <> 0
                                    links[i] ! [Memory FROM load.address FOR operand]
                                  TRUE
                                    SKIP
                            TRUE
                              SKIP
                    TRUE
                      SEQ
                        More := TRUE
                        -- The next processor(s) are to be booted !!! --
                        -- so build a bootable packet and output down link --
                        WHILE More
                          SEQ
                            bootlink.in ? [Memory FROM load.address FOR operand]
                            load.address := load.address PLUS operand
                            Bootstrap.length := Bootstrap.length PLUS operand
                            bootlink.in ? command
                            -- Stop building when a proper command is received This should --
                            -- be when a 'Bootstrap.end' is recieved
                            More := (INT command) < data.field
                            operand := (INT command) /\ data.field
              
                        SEQ i = 0 FOR 4
                          IF
                            (links.to.load /\ (1 << i)) <> 0
                              SEQ
                                links[i] ! (BYTE Bootstrap.length)
                                IF
                                  Bootstrap.length <> 0
                                    links[i] ! [Memory FROM Buffer.address FOR Bootstrap.length]
                                  TRUE
                                    SKIP
                            TRUE
                              SKIP
              
              --}}}
              --{{{  operand = BOOTSTRAP.END
              operand = Bootstrap.end
                SEQ
                  SEQ ii = 0 FOR Bootstrap.depth
                    SEQ
                      -- Pass on all the other bootstrap ends
                      bootlink.in ? command
                      SEQ i = 0 FOR 4
                        IF
                          (links.to.load /\ (1 << i)) <> 0
                            links[i] ! command
                          TRUE
                            SKIP
                  Bootstrap.depth := 0
              
              --}}}
              --{{{  operand = MEMORY.SIZE
              operand = Memory.size
                --VAL Dummy.scalar IS 42 :
                --VAL [] BYTE Command.array RETYPES Dummy.scalar :
                --VAL Word.size IS SIZE Command.array :
                VAL Word.size IS ((INT((MOSTPOS INT) = #7FFF)) * 2) + ((INT((MOSTPOS INT) <>  #7FFF)) * 4) :
                SEQ
                  IF
                    Word.size = 2
                      INT32 Memory.size :
                      SEQ
                        Get.encoded.value.32 (Memory.size)
                        Clear.memory.32 (Memory.size, MEMORY.PATTERN)
                    TRUE
                      INT Memory.size :
                      SEQ
                        Get.encoded.value (Memory.size)
                        Clear.memory (Memory.size, MEMORY.PATTERN)
              
              --}}}
              --{{{  ELSE
              TRUE
                SEQ
                  loading := TRUE
                  links.to.load := 0
              --}}}
          
          --}}}
          --{{{  Tag = number 
          TRUE
            SEQ
              output.link := operand
              links.to.load := links.to.load \/ (1 << output.link)
          --}}}
    
        bootlink.in ? command
    --}}}
:

